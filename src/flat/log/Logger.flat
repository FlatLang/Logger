package flat/log

import static flat/colorizer/Colorizer

class {
  let static Int TRACE = 5
  let static Int DEBUG = 4
  let static Int INFO = 3
  let static Int WARN = 2
  let static Int ERROR = 1
  let static Int OFF = 0

  var static Int DEFAULT_LOGGING_LEVEL = INFO
  var static Bool DEFAULT_SHOW_PREFIX = false
  var static Bool DEFAULT_SHOW_FORMATTING = false

  static {
    if (let level = System.getEnv("DEFAULT_LOGGING_LEVEL")) {
      let levelValue = match (level) {
        "TRACE" => TRACE
        "DEBUG" => DEBUG
        "INFO" => INFO
        "WARN" => WARN
        "ERROR" => ERROR
      }

      DEFAULT_LOGGING_LEVEL = levelValue
    }
    if (let showPrefix = System.getEnv("DEFAULT_SHOW_PREFIX")) {
      DEFAULT_SHOW_PREFIX = Bool.fromString(showPrefix)
    }
    if (let showFormatting = System.getEnv("LOGGING_DEFAULT_SHOW_FORMATTING")) {
      DEFAULT_SHOW_FORMATTING = Bool.fromString(showFormatting)
    }
  }

  let static Logger DEFAULT = Logger()

  let Int _loggingLevel = null
  let Int loggingLevel {
    get {
      return if (_loggingLevel != null) {
        _loggingLevel
      } else {
        DEFAULT_LOGGING_LEVEL
      }
    }
  }

  let Bool _showPrefix = null
  let Bool showPrefix {
    get {
      return if (_showPrefix != null) {
        _showPrefix
      } else {
        DEFAULT_SHOW_PREFIX
      }
    }
  }

  let Bool _showFormatting = null
  let Bool showFormatting {
    get {
      return if (_showFormatting != null) {
        _showFormatting
      } else {
        DEFAULT_SHOW_FORMATTING
      }
    }
  }

  public construct(
    private Class loggerClass = null,
    private String: label = null,
    Int: loggingLevel = null,
    Bool: showPrefix = null
  ) {
    _loggingLevel = loggingLevel
  }

  getPrefix(Int level) -> String {
    if (!showPrefix) {
      return ""
    }

    let levelString = match (level) {
      TRACE => "TRACE"
      DEBUG => "DEBUG"
      INFO => "INFO"
      WARN => "WARN"
      ERROR => "ERROR"
    }

    let classPrefix = loggerClass ? loggerClass.location + " " : ""
    let labelPrefix = label ? label + " " : ""

    let suffix = " #{classPrefix}#{labelPrefix}".trimEnd()
    let content = "[#{levelString}]" + suffix

    return if (showFormatting) {
      let colored = match (level) {
        TRACE => cyanBackground(content)
        DEBUG => greenBackground(content)
        INFO => content
        WARN => yellowBackground(content)
        ERROR => redBackground(content)
      }

      colored + " - "
    } else {
      content + " - "
    }
  }

  logMessage(String message, String level, Bool newLine = true) {
    throw Exception("Not implemented")
  }

  formatContent(String content, Int level) -> String {
    if (!showFormatting) {
      return content
    }

    let formatted = match (level) {
      TRACE => cyan(content)
      DEBUG => green(content)
      INFO => content
      WARN => yellow(content)
      ERROR => red(content)
    }

    return formatted
  }

  public trace(Object value, Bool: newLine = true) {
    if (loggingLevel >= TRACE) {
      let content = formatContent(value.toString(), TRACE)
      logMessage(getPrefix(TRACE) + content, "log", newLine)
    }
  }

  public debug(Object value, Bool: newLine = true) {
    if (loggingLevel >= DEBUG) {
      let content = formatContent(value.toString(), DEBUG)
      logMessage(getPrefix(DEBUG) + content, "log", newLine)
    }
  }

  public info(Object value, Bool: newLine = true) {
    if (loggingLevel >= INFO) {
      let content = formatContent(value.toString(), INFO)
      logMessage(getPrefix(INFO) + content, "log", newLine)
    }
  }

  public warn(Object value, Bool: newLine = true) {
    if (loggingLevel >= WARN) {
      let content = formatContent(value.toString(), WARN)
      logMessage(getPrefix(WARN) + content, "warn", newLine)
    }
  }

  public error(Object value, Bool: newLine = true) {
    if (loggingLevel >= ERROR) {
      let content = formatContent(value.toString(), ERROR)
      logMessage(getPrefix(ERROR) + content, "error", newLine)
    }
  }
}